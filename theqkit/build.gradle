apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.github.dcendents.android-maven'

androidExtensions {
  experimental = true
}

group = POM_GROUP_ID
version = "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"

android {
  compileSdkVersion 28

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 1
    versionName version

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  compileOptions {
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    debug {
      minifyEnabled false
      signingConfig signingConfigs.debug
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1'

  implementation 'com.squareup.retrofit2:retrofit:2.4.0'
  implementation 'com.squareup.okhttp3:okhttp:3.11.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
  implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
  implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
  implementation 'com.google.code.gson:gson:2.8.2'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
  implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
  kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"
  implementation 'com.google.android.exoplayer:exoplayer-core:2.9.6'
  implementation 'com.google.android.exoplayer:exoplayer-ui:2.9.6'
  implementation 'com.google.android.exoplayer:extension-rtmp:2.9.6'
  implementation 'com.tylerjroach:eventsource:1.2.11'
  implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
  implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
  implementation 'com.airbnb.android:lottie:2.8.0'
  // Koin for Android
  implementation 'org.koin:koin-android:1.0.2'
  // Koin for Android Architecture ViewModel
  implementation 'org.koin:koin-androidx-viewmodel:1.0.2'
}

dokka {
  outputFormat = 'javadoc'
  outputDirectory = "$buildDir/javadoc"
  packageOptions {
    prefix = "live.stream.theq.theqkit.data.app"
    suppress = true
  }
  packageOptions {
    prefix = "live.stream.theq.theqkit.extensions"
    suppress = true
  }
  packageOptions {
    // note: some Kotlin features (like type aliases and top-level functions) produce an auto-
    // generated class that cannot be @suppress'd. the `internal` package is primarily used
    // as a bucket that we can @suppress wholesale.
    prefix = "live.stream.theq.theqkit.internal"
    suppress = true
  }
  packageOptions {
    prefix = "live.stream.theq.theqkit.player"
    suppress = true
  }
  packageOptions {
    prefix = "live.stream.theq.theqkit.ui.game"
    suppress = true
  }
}

task javadocJar(type: Jar, dependsOn: dokka) {
  classifier = 'javadoc'
  from "$buildDir/javadoc"
}

artifacts {
  archives javadocJar
}